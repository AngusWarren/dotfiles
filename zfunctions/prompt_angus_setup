#
# A single line theme with Git information on the left and Ruby on the right.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#
# Screenshots:
#   http://i.imgur.com/gLgVp6Y.png
#

function prompt_angus_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_angus_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_angus_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_angus_colors=(
      "%F{cyan}"  # Turquoise
      "%F{yellow}" # Orange
      "%F{blue}" # Purple
      "%F{red}" # Hotpink
      "%F{blue}" # Limegreen
    )
  else
    _prompt_angus_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{blue}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${_prompt_angus_colors[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${_prompt_angus_colors[5]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${_prompt_angus_colors[2]}●%f"
  zstyle ':prezto:module:git:info:modified' format "${_prompt_angus_colors[4]}●%f"
  zstyle ':prezto:module:git:info:untracked' format "${_prompt_angus_colors[3]}●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '[%v]'

  # Define prompts.
  PROMPT="${_prompt_angus_colors[3]}%n%f@${_prompt_angus_colors[2]}%m%f ${_prompt_angus_colors[5]}%~%f "'${git_info:+${(e)git_info[prompt]}}'"» "
  RPROMPT='%F{blue}${ruby_info[version]}'
}


#prompt_status() {
#  local symbols
#  symbols=()
#  [[ $RETVAL -ne 0 ]] && symbols+="%{%F{red}%}$CROSS"
#  [[ $UID -eq 0 ]] && symbols+="%{%F{yellow}%}$LIGHTNING"
#  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{cyan}%}$GEAR"
#
#  [[ -n "$symbols" ]] && prompt_segment $PRIMARY_FG default " $symbols "
#}


prompt_angus_setup "$@"
